-- programs ------------------------------------------------

entrypoints Program ;

Prog.      Program ::= [TopDef] ;

-- A TopDef is either a type declaration or a top level function or a method.

-- Function
TopFnDef.    TopDef ::= FnDef;
FunDef.      FnDef  ::= Type Ident "(" [Arg] ")" Block ;

-- Method
internal MethodDef. FnDef   ::= Type Ident Ident Arg [Arg] Block;

-- Type Declaration
TopTypeDecl. TopDef ::= TypeDecl;

StructDef. TypeDecl ::= "struct" Ident "{" [SField] "}"";";
PtrDef.    TypeDecl ::= "typedef" "struct" Type "*" Ident";";
ClassDef.  TypeDecl ::= "class" Ident Hierarchy "{" [SField] [FnDef] "}";

HExtend.    Hierarchy ::= "extends" Ident;
HEmpty.     Hierarchy ::= ;

separator nonempty TopDef "" ;
separator nonempty FnDef "";

Argument.  Arg ::= Type Ident;

separator  Arg "," ;

StrField.  SField ::= Type Ident;

terminator  SField ";";

-- statements ----------------------------------------------

SBlock.    Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= LVal "=" Expr  ";" ;
LValVar.   LVal ::= Ident [DimA]; 
LValStr.   LVal ::= Ident "->" Ident;
internal LValTyped. LVal ::= "[" LVal ":" Type "]";

Incr.      Stmt ::= LVal "++"  ";" ;

Decr.      Stmt ::= LVal "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

For.       Stmt ::= "for" "(" IDecl ":" Expr ")" Stmt;

ForDecl.   IDecl::= Type Ident;
-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Doub.      Type ::= "double" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Dimension. Dim  ::= "[]";

Array.     Type ::= Type [Dim];

Ref.       Type ::= Ident;

separator Type "";

internal Pointer.   Type ::= Ident "*";
internal Object.    Type ::= Ident "[|" [Ident] "|]";

separator Ident ":";
separator nonempty Dim "";

internal Struct. Type  ::= Ident [SField];

internal   String. Type ::= "String" ;
internal DimT.   Type   ::= "[<" Type Integer ">]";
-- Expressions ---------------------------------------------

Var.        Expr6 ::= Ident [DimA] ;

Method.     Expr6 ::= Expr6 "." Expr6;

DimAddr.   DimA  ::= "[" Expr "]" ;

separator  DimA "";

-- Array initialization
ENew.      Expr6 ::= "new" Type [DimA] ;

PtrDeRef.  Expr6 ::= Ident "->" Ident;

-- Shift-reduce
ENull.     Expr6 ::= "(" Ident ")" "null";

internal NullC. Expr ::= "null";

ELitInt.   Expr6 ::= Integer ;

ELitDoub.  Expr6 ::= Double;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")";
internal   MApp. Expr ::= "." Ident "(" Expr "," [Expr] ")" ;
EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;


coercions  Expr 6 ;

separator  Expr "," ;

internal ETyped. Expr ::= "[" Expr ":" Type "]";


-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
