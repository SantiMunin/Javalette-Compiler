-- programs ------------------------------------------------

entrypoints Program ;

Prog.      Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

StructDef. TopDef ::= "struct" Ident "{" [SField] "}"";";

PtrDef.    TopDef ::= "typedef" "struct" Type "*" Ident";";

separator nonempty TopDef "" ;

Argument.  Arg ::= Type Ident;

separator  Arg "," ;

StrField.  SField ::= Type Ident;

separator  SField ";";

-- statements ----------------------------------------------

SBlock.    Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= LVal "=" Expr  ";" ;
LValVar.   LVal ::= Ident [DimA]; 
LValStr.   LVal ::= Ident "->" Ident;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

For.       Stmt ::= "for" "(" IDecl ":" Expr ")" Stmt;

ForDecl.   IDecl::= Type Ident;
-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Doub.      Type ::= "double" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Dimension. Dim  ::= "[]";

Array.     Type ::= Type [Dim];

Str.       Type ::= Ident;

separator nonempty Dim "";

internal Struct. Type  ::= Ident [SField];

internal Pointer. Type ::= Ident;

internal   String. Type ::= "String" ;

-- Expressions ---------------------------------------------

Var.       Expr6 ::= Ident [DimA];

Method.    Expr6 ::= Ident [DimA] "." Ident;

DimAddr.   DimA  ::= "[" Expr "]";

separator  DimA "";


-- Array initialization
ENew.      Expr6 ::= "new" Type [DimA] ;

PtrDeRef.  Expr6 ::= Ident "->" Ident;

-- Shift-reduce
ENull.     Expr6 ::= "(" Ident ")" "null";

internal   NullC. Expr ::= Type;

ELitInt.   Expr6 ::= Integer ;

ELitDoub.  Expr6 ::= Double;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;


coercions  Expr 6 ;

separator  Expr "," ;

internal ETyped. Expr ::= "[" Expr ":" Type "]";
internal DimT.   Type ::= "DimT" Type Integer;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
